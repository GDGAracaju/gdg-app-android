apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'


def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    bundle{
        density.enableSplit = true
        abi.enableSplit = true
        language.enableSplit = false
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "gdg.aracaju.news"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 2
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            shrinkResources false
            crunchPngs false
            applicationIdSuffix ".debug"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/atomicfu.kotlin_module'
    }
}

dependencies {

    implementation Dependencies.kotlinLib
    implementation Dependencies.appCompat
    implementation Dependencies.ktx
    implementation Dependencies.constraintLayout
    implementation Dependencies.cardView
    implementation Dependencies.recyclerView
    implementation Dependencies.groupie
    implementation Dependencies.groupieKotlin
    implementation Dependencies.firebaseCore
    implementation Dependencies.coroutine
    implementation Dependencies.coroutineAndroid
    implementation Dependencies.viewModel
    implementation Dependencies.aacLifecycleJava8
    implementation Dependencies.material
    implementation Dependencies.firebaseDataBase
    implementation Dependencies.picasso
    implementation Dependencies.maps
    implementation Dependencies.threetenabp
    implementation Dependencies.firebaseAuth
    implementation Dependencies.googleAuth
    implementation Dependencies.notification
    implementation Dependencies.kodein
    implementation Dependencies.kodeinConf

    testImplementation DependenciesTest.junit4
    androidTestImplementation DependenciesTest.androidRunner
    androidTestImplementation DependenciesTest.espressoCore
}
apply plugin: 'com.google.gms.google-services'
